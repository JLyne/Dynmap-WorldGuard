import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url 'https://repo.mikeprimm.com/'
    }

    maven {
        url 'https://maven.enginehub.org/repo/'
    }
}

dependencies {
    compileOnly 'us.dynmap:DynmapCoreAPI:3.7-SNAPSHOT'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.13'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.3.13'
    compileOnly 'io.papermc.paper:paper-api:1.21.5-R0.1-SNAPSHOT'
}

group = 'org.dynmap'
version = '1.3-SNAPSHOT'

java.sourceCompatibility = JavaVersion.VERSION_21
java.targetCompatibility = JavaVersion.VERSION_21

tasks {
    processResources {
        from("${project.rootDir}/src/main/resources") {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
            project.properties.findAll().each {
                prop ->
                    if (prop.value != null && prop.value instanceof java.lang.String) {
                        filter(ReplaceTokens, tokens: [(prop.key): prop.value])
                        filter(ReplaceTokens, tokens: [('project.' + prop.key): prop.value])
                    }
            }
        }
    }

    compileJava {
        options.compilerArgs += ["-Xlint:all", "-Xlint:-processing"]
        options.encoding = 'UTF-8'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}
